GAL22V10
glue

;
; use galasm to compile to jed
; https://github.com/daveho/GALasm
;
; RST: external reset
;

A19 A18  A17   A16   A15   AS    DS    FC0  RW     FC1   FC2  GND
RST HALT DTACK ROMOE RAMWE RAMOE RESET IACK UARTCS VIDRW VIDM VCC

RESET.T = RST
RESET.E = /RST

HALT = RST

DTACK.T = AS
DTACK.E = RST * /AS * /A15

/IACK = RST * /AS * FC2 * FC1 * FC0

/ROMOE  = RST * /AS *  RW  * /A15 * /A16 * /A17               ; $00000
/RAMOE  = RST * /AS *  RW  * /A15 *  A16 * /A17               ; $10000
/RAMWE  = RST * /AS * /RW  * /A15 *  A16 * /A17               ; $10000
/UARTCS = RST * /AS *         A15 *  A16 * /A17 * /FC1 * FC0  ; $18000

DESCRIPTION

mini68k glue logic

